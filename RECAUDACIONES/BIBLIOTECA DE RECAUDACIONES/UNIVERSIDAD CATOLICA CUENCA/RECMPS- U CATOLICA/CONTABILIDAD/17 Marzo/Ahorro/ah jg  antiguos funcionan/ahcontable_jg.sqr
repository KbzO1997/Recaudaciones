!************************************************************************!
!*      Archivo           :  ah_contable.sqr                            *!
!*      Base de datos     :  cob_ahorros                                *!
!*      Producto          :  Cuentas de Ahorros                         *!
!*      Disenado por      :  Walter Marmol                              *!
!*      Fecha de escritura:  27/Sept/95                                 *!
!************************************************************************!
!*                              IMPORTANTE                              *!
!*      Este programa es parte de los paquetes bancarios propiedad del  *!
!*      "BANCO BOLIVARIANO".                                            *!
!*      Su uso no autorizado queda expresamente prohibido asi como      *!
!*      cualquier alteracion o agregado hecho por alguno de sus         *!
!*      usuarios sin el debido consentimiento por escrito de la         *!
!*      Presidencia Ejecutiva del BANCO BOLIVARIANO o su representante. *!
!************************************************************************!
!*                              PROPOSITO                               *!
!*      Reporte diario de comprobantes contables del repositorio de     *!
!*      de perfiles                                                     *!
!************************************************************************!
!*                            MODIFICACIONES                            *!
!*      FECHA           AUTOR           RAZON                           *!
!*      27/Sept/95    W.Marmol         Emision Inicial                  *! 
!*      23/Jun/97     D.Sanchez        Acoplarlo a Cuentas de Ahorros.  *!
!*      01/Ago/2000   R. Egas           Cambio a moneda Base            *!
!*	05/Jun/2003	Galo Yanez	Direccionar a tablas de ahorros *!
!*   #  14.Oct.2003  Galo Yanez  Cambiar older sqr commands.            *!
!*      20.AGO.2006  Paul Hoyos V.     Se cambiar el formato del número *!
!*                                     de la cotización.                *!   
!************************************************************************!

#include  "definic.inc"

#define   ANCHO_PAG                132 !Ancho de la pagina a imprimir
#define   LARGO_PAG                 62 !Largo de la pagina a imprimir
#define  SALIDA  '<37><33><10>(1ups.jdt) STARTLM<10>'

Begin-Setup
use cob_conta
End-Setup  

#include "printer_1ups.inc"   !REF:GYC

#include "log.inc"
#include "conta.inc"

Begin-Program   !REF:GYC
do Inicializar_Programa
do Parametros_Generales
let $cond1 = 'pd_producto = '
!do ParamN (1,#i_producto,'cobis..cl_producto',$cond1,'No existe producto')
!do ParamS (2,$i_fecha,'','','')

let #i_producto = 4
let $i_fecha = '02/25/2021'

do Main
do Finalizar_Programa
End-Program   !REF:GYC

Begin-Heading 8 
let $titulo = '** VALIDACION DE COMPROBANTES CONTABLES **'
let $subtit = 'PERFILES CONTABLES'
do Cabecera1 ($titulo,$subtit,$i_fecha,$nombre_empresa,$nombre_oficina,'',$nombre_programa)
position (+1)
do Lin_Horizontal
do Centrar ('CUENTA',1)
do Centrar ('MON.',2) 
do Centrar ('ORIG.',3) 
do Centrar ('DEST.',4)
do Derecha ('DEBITO',5)
do Derecha ('CREDITO',6)
do Derecha ('DEBITO ME',7)
do Derecha ('CREDITO ME',8)
do Derecha ('TIPO DE CAMBIO',9)
do Lin_Horizontal
End-Heading

Begin-Procedure Main
do Nombre_Empresa  (#i_empresa,$nombre_empresa,#moneda_base)
do Nombre_Programa ($nombre_programa)
do Columnas (15,5,5,5,18,18,18,18,18,0,0,0,0,0,0,0,0,0,0,0)
do Seleccion 
End-Procedure

Begin-Procedure Seleccion
!* Selecciona los comprobantes emitidos a la fecha *!

let #break = 0
Begin-Select
sc_oficina_orig () on-break print=never level=1 before=Before_OfArea after=After_OfArea
of_descripcion
of_oficina
sc_area_orig
pe_perfil       () on-break print=never level=2 before=Before_Tran 
pe_descripcion
sc_comprobante  () on-break print=never level=3 before=Before_Comp after=After_Comp
sc_descripcion
sc_producto
sc_tot_debito 
sc_tot_credito
sc_tot_debito_me
sc_tot_credito_me
sc_estado         ! I=Ingresado A=Anulado  P=Procesado E=Errado   * WM970217
sa_cuenta
sa_moneda
sa_oficina_dest
sa_debito
sa_credito
sa_debito_me
sa_credito_me
sa_cotizacion
sa_concepto 
   do PrintS (1,&sa_cuenta,'',0)
   do PrintN (2,&sa_moneda,'999',0)
   do PrintN (3,&sc_oficina_orig,'999',0)
   do PrintN (4,&sa_oficina_dest,'999',0)
   do PrintN (5,&sa_debito,'{FOR_DIN}',0)
   do PrintN (6,&sa_credito,'{FOR_DIN}',0)
   do PrintN (7,&sa_debito_me,'{FOR_DIN}',0)
   do PrintN (8,&sa_credito_me,'{FOR_DIN}',0) 
   do PrintN (9,&sa_cotizacion,'9,999.9999',0)    !PHV  08AGO2006
   next-listing
   let #break = 0
   do Actualizar_Reg_Proc
!--from cb_scomprobante,cb_sasiento,cb_oficina,cb_perfil			!-- GYC 2003/Jun/05
from cob_ahorros..ah_scomprobante, cob_ahorros..ah_sasiento, cb_oficina, cb_perfil
where sc_empresa        = convert(tinyint,#i_empresa) and
      sc_producto       = convert(tinyint,#i_producto) and
      sc_fecha_tran     = $i_fecha       and
      (sc_estado        = 'I' or             
       sc_estado        = 'P' or				!*WM970217
       sc_estado        = 'A' or				!*WM970217
       sc_estado        = 'E')           and                    !*WM970217
      sc_empresa        = sa_empresa     and
      sc_producto       = sa_producto    and
      sc_fecha_tran     = sa_fecha_tran  and
      sc_comprobante    = sa_comprobante and
      sc_empresa        = pe_empresa     and
      sc_producto       = pe_producto    and
      sc_perfil         = pe_perfil      and
      sc_empresa        = of_empresa     and
      sc_oficina_orig   = of_oficina
order by sc_oficina_orig,pe_perfil,sc_comprobante,sa_asiento
End-Select
End-Procedure

Begin-Procedure Before_OfArea
let $nombre_oficina = &of_descripcion || ' (' || to_char(&of_oficina) || ')'
End-Procedure

Begin-Procedure After_OfArea
add 1 to #break 
if #break = 1
   new-page
end-if
End-Procedure

Begin-Procedure Before_Tran
! print 'TRANSACCION ' (+2,1) 
! print &pe_descripcion ()
! next-listing
! next-listing
End-Procedure

Begin-Procedure Before_Comp
print 'COMPROBANTE' (+2,1) 
print &sc_oficina_orig      () edit 999
print &sc_area_orig         () edit 999
print &sc_comprobante       () edit 99999
!print &sc_descripcion       (0,+4)
print &sa_concepto           (0,+4)
if &sc_estado = 'A'    		!* INCLUIR TODOS LOS ESTADOS	*WM970217
   print '* ANULADO *'      (+1,1) 
else
   if &sc_estado = 'P'
      print '* PROCESADO*'     (+1,1) 
   else
      if &sc_estado = 'E'
         print '* ERRADO *'      (+1,1) 
      else
         if &sc_estado = 'I'
            print '* INGRESADO *'    (+1,1) 
	 end-if
      end-if
   end-if
end-if
let #sc_tot_debito      = &sc_tot_debito
let #sc_tot_credito     = &sc_tot_credito
let #sc_tot_debito_me   = &sc_tot_debito_me
let #sc_tot_credito_me  = &sc_tot_credito_me
next-listing
next-listing
End-Procedure

Begin-Procedure After_Comp
do Derecha ('---------------',5)
do Derecha ('---------------',6)
do Derecha ('---------------',7)
do Derecha ('---------------',8)
next-listing
do PrintS (2,'TOTAL : ','',0)
do PrintN (5,#sc_tot_debito,'{FOR_DIN}',0)
do PrintN (6,#sc_tot_credito,'{FOR_DIN}',0)
do PrintN (7,#sc_tot_debito_me,'{FOR_DIN}',0)
do PrintN (8,#sc_tot_credito_me,'{FOR_DIN}',0)
next-listing
next-listing
End-Procedure
  
Begin-Procedure Parametros_Generales
!  /*** codigo de la filial/banco ***/
!begin-select
!pa_tinyint &pa_tinyinta
!   let #i_empresa = &pa_tinyinta
!from cobis..cl_parametro
!where pa_nemonico = 'FLL'
!and pa_producto = 'CEX'
!end-select
let #i_empresa = 1            !DSC970623
End-Procedure

Begin-Procedure Cabecera1 ($titulo,$subtitulo1,$fecha_emision,$empresa,$oficina,
                          $moneda,$archivo)
let #pos = {ANCHO_PAG} - 23 
BEGIN-SELECT 
convert(char(10),getdate(),101) &fecha_actual
END-SELECT
!date-time () 'MM/DD/YYYY' &fecha_actual
do Mes_Espaniol ($fecha_emision,$fecha_emi)
do Mes_Espaniol (&fecha_actual,$fecha_imp)
print $empresa                             (2,1)
print $titulo                              (2) center
let $fecha_imp = 'FECHA IMP.: ' || $fecha_imp
print $fecha_imp                           (2,#pos)      
print $oficina                             (3,1)
print $subtitulo1                          (3) center
page-number  (3,#pos) 'PAG.      : '
print $moneda                              (4,1)
print $fecha_emi                           (4) center
print $archivo                             (4,#pos)
End-Procedure
