!************************************************************************!
!*      Archivo:                pgccclaro.sqr                           *!
!*      Base de datos:          cob_pagos                               *!
!*      Producto:               Cuentas Corrientes                      *!
!*      Disenado por:           Daniel Pereira                          *!
!*      Fecha de escritura:     15/Sep/2011                             *!
!************************************************************************!
!*                              IMPORTANTE                              *!
!*      Este programa es parte de los paquetes bancarios propiedad de   *!
!*      "BANCO BOLIVARIANO"                                             *!
!*      Su uso no autorizado queda expresamente prohibido asi como      *!
!*      cualquier alteracion o agregado hecho por alguno de sus         *!
!*      usuarios sin el debido consentimiento por escrito de la         *!
!*      Presidencia Ejecutiva de BANCO BOLIVARIANO.                     *!
!************************************************************************!
!*                              PROPOSITO                               *!
!*      Este programa carga  el archivo de CLARO y concilia los         *!
!*      pagos realizados en Banco Bolivariano                           *!
!************************************************************************!
!*                              MODIFICACIONES                          *!
!*      FECHA           AUTOR             RAZON                         *!
!*   1  15/Sep/2011   Daniel Pereira V.   MTAC-CU-1729 y  MTAC-CU-1730 Emision Inicial  *!
!*   2  19/Ene/2012   Daniel Pereira V.   CTE-CC-10580 cambio en proceso*!
!*                                        de carga para feriado         *!
!*   3  02/Feb/2013   Daniel Pereira V.   CTE-CE-SGC00007428            *!
!*   4  19/Jun/2013   Daniel Pereira V.   CTE-CC-SGC00009721 NOMBRE ARCHIVO CLARO *!
!*   5  03/Ene/2014   Daniel Pereira V.   CTE-CE-SGC00013181            *!
!*   6  25/Sep/2019   Maria Jose Silva    RECM-25			            *!
!*   7  12/Abr/2024   Kevin Bastidas Z.   RECM-891			            *!
!************************************************************************!

#define  MAXLEN_NOM_CLIENTE 60    !Longitud del campo nombre del cliente
#define  MAXLEN_DIR_CLIENTE 60    !Longitud del campo direccion del cliente
#define  MAXLEN_SALDO_TOTAL 13
#define  MAXLEN_VALOR_EMISION 13
#define  MAXLEN_RETENCION 13
#define  WPATH      '/respaldo/archivos/cuentas/data/'

#include "definic.inc"


Begin-Setup
use cob_pagos
End-Setup

#include "log.inc"
#include "ctacte.inc"

Begin-Program
  do Inicializar_Programa
  do ParamS(1,$i_fecha_proceso,'','','')

  let $dia = substr($i_fecha_proceso,4,2)
  let $mes = substr($i_fecha_proceso,1,2)
  let $anio = substr($i_fecha_proceso,7,4)

begin-select !REF 4
ltrim(rtrim(pa_char))    &valor
  let  $valor = &valor
from cobis..cl_parametro
where pa_nemonico = 'ARCCTA'
and pa_producto = 'CTE'
end-select


  let $ruta_archivo = '/respaldo/archivos/cuentas/data/'
  !let $name_file = 'Porta' || $mes || $dia || '.txt'
  let $name_file = $valor || $mes || $dia || $anio || '.txt'
  let $i_archivo_origen = $ruta_archivo  || $name_file

  let #num_reg_canal = 1

  do Main
  do Finalizar_Programa
End-Program


Begin-Procedure Main
  do Limpia_tabla
  do Procesa_Cargar
  do Crear_Temp
  do Conciliar
End-Procedure


Begin-Procedure Limpia_tabla
!Elimina lo cargado Según la fecha de proceso
Begin-Sql
  delete pg_concilia_claro
  where co_fec_cble   = $i_fecha_proceso
End-Sql
End-Procedure

Begin-Procedure Procesa_Cargar !REF #2
let $flag = 'N'
let $w_fecha_proceso = $i_fecha_proceso

while $flag = 'N'

do Cargar_Data

!temporal
begin-select
convert(varchar(10),DATEADD(day, -1, $w_fecha_proceso),101) &w_fecha_proceso
end-select

  let $w_fecha_proceso = &w_fecha_proceso

  execute  cob_cuentas..sp_identificadia
         @i_fechahoy   = $w_fecha_proceso,
         @i_tipo       = 'S',
         @o_compensa   = $feriado  out

  if $feriado = 'NO'
    let $flag = 'N'
  else
    let $flag = 'S'
  end-if

  let $dia1 = substr($w_fecha_proceso,4,2)
  let $mes1 = substr($w_fecha_proceso,1,2)
  let $anio1 = substr($w_fecha_proceso,7,4) !REF 5
  !let $name_file1 = 'Porta' || $mes1 || $dia1 || '.txt'
  let $name_file1 = $valor || $mes1 || $dia1 ||$anio1|| '.txt'
  let $i_archivo_origen = $ruta_archivo  || $name_file1

end-while

End-Procedure


Begin-Procedure Cargar_Data
!-------------------------------------*
!---ELIMINACION DE CARGA EXISTENTE----*
!-------------------------------------*

open $i_archivo_origen as 1 for-reading record=176 status=#openfileori

if #openfileori <> 0
    show ' '
    show ' ERROR; En Apertura del Archivo ' $i_archivo_origen
    do Abortar
else
    show ' '
    show ' CARGANDO DETALLE DEL ARCHIVO PARA CONCILIACION DE CLARO........' $i_archivo_origen
    let #contador = 0
    let #cont_cab = 0
    let #flag = 0
    let #cant_trx = 0


    while #flag = 0
        read 1  into $cadena:176 !171 Ref006:msilvag
        let $tipo_registro = substr($cadena,1,1)

        if $tipo_registro = '1' !LEE CABECERA

            if #cont_cab > 0
                break
            end-if

            !*Fecha de la Generación del Archivo
            let $fech_arch = substr ($cadena,2,8)

            !*Total de Transacciones
            let $cant_trx = substr ($cadena,18,8)

begin-select
convert(money,$cant_trx)  &cant_trx
convert(varchar,getdate(),101) &fecha_real
convert(varchar,getdate(),101) &fecha_contbl
end-select

           move &cant_trx to #cant_trx
           let #cont_cab = #cont_cab + 1

        else  !LEE DETALLE
				
            !*Tipo de Identificación
            let $identificacion  = substr($cadena,42,13)  !Ruc
            let $wruc = substr($identificacion,1,3)
               if $wruc = '000'
                    let $identificacion = substr($identificacion,4,10) !Cedula
               end-if

            !*Nombre del Cliente
            let $nombre =  substr ($cadena,2,39) !Aumentado verificar

            !* Número de Autorización del Banco
            let $autor_bco = substr ($cadena,90,13)

            !* Indicador de Reverso
            let $ind_rev = substr ($cadena,103,1)
            if $ind_rev = 'D'
               let $ind_rev = 'S'
            else
               let $ind_rev = 'N'
            end-if

           !* Valor Transaccion
           let $val_pag = substr ($cadena,104,15)
           let $val_pag = substr ($val_pag,1,13) || '.' || substr($val_pag,14,2)

           !*Número de Cuenta
           let $cuenta = substr ($cadena,119,10)

           !*Tipo de Cuenta
           let $tipo_cta = substr ($cadena,129,2)

           !*Fecha Transaccion
           let $fecha_trx = substr ($cadena,131,8)
           let $fecha_contbl = $i_fecha_proceso !fecha que ejecutan el proceso

           !*Número de Transaccion
           let $id_trx_emp = substr ($cadena,139,10)

           !*Canal
           let $canal = substr ($cadena,156,3)

           !*Valor Iva
           let $val_iva = substr ($cadena,159,12)
           let $val_iva = substr ($val_iva,1,9) || '.' || substr($val_iva,10,4)

           let #val_pag = $val_pag
           let #tot_pag = #tot_pag + #val_pag
           let #val_iva = $val_iva
           let #autor_bco = $autor_bco
			
			!Ref006:msilvag Inicio
			let $empresa_claro = substr ($cadena,172,5)
           	
           	if $empresa_claro = '00150' !--KBastidz Ref 7 Update de 132 a 150
           		let $empresa = '40'
           	else
           		let $empresa = '39'
           	end-if
			!Ref006:msilvag Fin
        end-if

      if #end-file
         let #flag = 1

         if #contador <> #cant_trx
            show 'archivo descuadrado cant. reg en cabecera ' #cant_trx ' Diferente cant. reg. detalle ' #contador
            do Abortar
         end-if

          break
      else

      if $tipo_registro =  '2'  !INSERTA TABLA
             begin-sql
             insert into pg_concilia_claro
             (co_ced_ruc      ,co_sucursal      ,co_autoriz_bco   ,co_valor     ,co_fec_cble    ,co_fecha_trx      ,
              co_hora_trn     ,co_canal         ,co_nombre        ,co_celular   ,co_reverso     ,co_num_trx_emp    ,
              co_nro_cuenta   ,co_tipo_cuenta   ,co_fecha_archivo ,co_valor_iva ,co_fecha_real  ,co_empresa		)!Ref006:msilvag
             values
             ($identificacion ,null             ,#autor_bco       ,#val_pag     ,$fecha_contbl  ,$fecha_trx        ,
              null            ,$canal           ,$nombre          ,null         ,$ind_rev       ,$id_trx_emp       ,
              $cuenta         ,$tipo_cta        ,$fech_arch       ,#val_iva     ,&fecha_real    ,$empresa 		)!Ref006:msilvag
             end-sql

         !Contador de Registros
         let #contador = #contador + 1
        end-if
      end-if

   end-while

   show ''
   let $contador = edit (to_char(#contador),'99999')
   show 'Se cargaron: ' $contador ' registros de detalle.'
   show 'CARGA DE ARCHIVO CONCILIA DE CLARO OK ...'

end-if
close 1
End-Procedure

Begin-Procedure Crear_Temp
!---------------------------------------------*
!---CREA TABLA CON DATOS DE LA TRAN_SERVICIO--*
!---------------------------------------------*
show 'CREANDO TABLA TEMPORAL CLARO CON MOVIMIENTOS DE TRAN_SERVICIO.....'
execute sp_concilia_claro_b
@i_opcion = 'W',
@i_fecha = $i_fecha_proceso,
@i_fecha1 = $i_fecha_proceso
if #w_return <> 0
    do Abortar
end-if
show 'TABLA TEMPORAL CREADA'
End-Procedure

Begin-Procedure Conciliar
show 'REALIZANDO CONCILIACION DE CLARO ........'
execute sp_concilia_claro_p
@i_opcion = 'P',
@i_fecha  = $i_fecha_proceso
if #w_return <> 0
   do Abortar
end-if
show 'CONCILIACION REALIZADA OK'
End-Procedure

